plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

ext {
    javafxVersion = '17.0.10'
    def os = org.gradle.internal.os.OperatingSystem.current()
    javafxPlatform = os.isWindows() ? 'win' : (os.isMacOsX() ? 'mac' : 'linux')
}

dependencies {
    implementation "org.openjfx:javafx-controls:${javafxVersion}:${javafxPlatform}"
    implementation 'org.xerial:sqlite-jdbc:3.45.2.0'
    testImplementation platform('org.junit:junit-bom:5.11.3')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

javafx {
    version = javafxVersion
    modules = ['javafx.controls']
}

application {
    mainClass = 'org.example.Launcher'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.register('syncJavafxLibs', Sync) {
    from {
        configurations.runtimeClasspath.filter { file ->
            file.name.startsWith('javafx') && file.name.endsWith('.jar')
        }
    }
    into layout.buildDirectory.dir('javafx')
}

tasks.named('run').configure {
    dependsOn(tasks.named('syncJavafxLibs'))
}

tasks.register('packageWithJdk', Copy) {
    group = 'distribution'
    description = 'Bundles the application with the current JDK.'

    dependsOn tasks.named('installDist')

    def jdkHome = System.getProperty("java.home")
    def installDir = layout.buildDirectory.dir("install/XiangqiwithJavaFX")
    def outputDir = layout.buildDirectory.dir("distribution/XiangqiGame")

    from(installDir) {
        into "."
    }

    from(jdkHome) {
        into "runtime"
        // Exclude unnecessary files to reduce size
        exclude "lib/src.zip", "demo/**", "man/**", "legal/**", "jmods/**"
    }

    into outputDir

    doLast {
        def batFile = new File(outputDir.get().asFile, "StartGame.bat")
        batFile.text = """@echo off
set "DIR=%~dp0"
set "JAVA_HOME=%DIR%runtime"
set "PATH=%JAVA_HOME%\\bin;%PATH%"
call "%DIR%bin\\\\XiangqiwithJavaFX.bat"
"""
    }
}
